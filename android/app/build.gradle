buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.marcellogalhardo.quandoo"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        buildTypes.each {
//            URL to customers list: https://s3-eu-west-1.amazonaws.com/quandoo-assessment/customer-list.json
//            URL to tables list: https://s3-eu-west-1.amazonaws.com/quandoo-assessment/table-map.json
//            it.buildConfigField "String", "YELP_CONSUMER_KEY", YELP_CONSUMER_KEY
        }
        debug {
            minifyEnabled false
            useProguard false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }
        release {
            minifyEnabled true
            useProguard true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }
    }

    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
            buildConfigField "String", "SERVER_URL", '"http://localhost:1234/"'
        }
        prod {
            buildConfigField "String", "SERVER_URL", '"https://s3-eu-west-1.amazonaws.com/quandoo-assessment/"'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true);
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }

    }

}

dependencies {
    // App's dependencies
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    // Google play services
    compile "com.google.android.gms:play-services-gcm:10.2.4"

    // RxJava dependencies
    compile 'io.reactivex.rxjava2:rxjava:2.0.7'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // Network dependencies
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    compile 'com.google.code.gson:gson:2.8.0'

    // Database dependencies
    compile 'com.orhanobut:hawk:2.0.1'

    // Ui dependencies
    compile 'uk.co.chrisjenx:calligraphy:2.2.0'

    // Dagger dependencies
    compile 'com.google.dagger:dagger:2.10'
    compile 'com.google.dagger:dagger-android:2.10'
    compile 'com.google.dagger:dagger-android-support:2.10'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.10'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.10'
    provided 'org.glassfish:javax.annotation:10.0-b28'

    // ButterKnife dependencies
    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'

    // Test dependencies
    androidTestCompile 'com.squareup.okhttp3:mockwebserver:3.6.0'
    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs'
    })
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile "org.robolectric:robolectric:3.1.1"
}
